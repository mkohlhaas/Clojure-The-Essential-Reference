(ns f.core
  (:import [java.time Duration Instant]
           [java.time.format DateTimeFormatter]
           [java.time.temporal TemporalAccessor]))

(partition     3 (range 10)) ; ((0 1 2) (3 4 5) (6 7 8))
(partition-all 3 (range 10)) ; ((0 1 2) (3 4 5) (6 7 8) (9))

(partition-by count (map str [12 11 8 2 100 102 105 1 3])) ; (("12" "11") ("8" "2") ("100" "102" "105") ("1" "3"))

(comment
  (partition 3 0 (range 10))) ; ((0 1 2) (0 1 2) (0 1 2) (0 1 2) (0 1 2) (0 1 2) (0 1 2) (0 1 2) (0 1 2) (0 1 2) â€¦)

(partition 3 3 (range 10)) ; ((0 1 2) (3 4 5) (6 7 8))
(partition 3 2 (range 10)) ; ((0 1 2) (2 3 4) (4 5 6) (6 7 8))

(partition 3 3 [:a :b :c] (range 10)) ; ((0 1 2) (3 4 5) (6 7 8) (9 :a :b))

(partition-all 3   (range 10)) ; ((0 1 2) (3 4 5) (6 7 8) (9))
(partition-all 3 2 (range 10)) ; ((0 1 2) (2 3 4) (4 5 6) (6 7 8) (8 9))

;; ;;;;;;;;;;;;;;;;;;;;;
;; Splitting SQL Inserts
;; ;;;;;;;;;;;;;;;;;;;;;

(def records (map #(-> {:id % :data (str %)}) (range 1000)))

(comment
  (map #(-> {:id % :data (str %)}) (range 10)))
  ; ({:id 0, :data "0"}
  ;  {:id 1, :data "1"}
  ;  {:id 2, :data "2"}
  ;  {:id 3, :data "3"}
  ;  {:id 4, :data "4"}
  ;  {:id 5, :data "5"}
  ;  {:id 6, :data "6"}
  ;  {:id 7, :data "7"}
  ;  {:id 8, :data "8"}
  ;  {:id 9, :data "9"})

(defn log [query] (str (.substring query 0 70) "...\n"))

(comment
  (log "0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 "))
  ; "0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26...\n"

(defn insert-query [records]
  (let [->value (fn [{:keys [id data]}] (format "(%s,%s)" id data))
        rows    (apply str (interpose "," (map ->value records)))]
    (log
     (str "INSERT INTO records (id, data) VALUES " rows
          " ON DUPLICATE KEY UPDATE"))))

(println (pmap insert-query (partition-all 10 records)))
; (out) (INSERT INTO records (id, data) VALUES (0,0),(1,1),(2,2),(3,3),(4,4),(5...
; (out)  INSERT INTO records (id, data) VALUES (10,10),(11,11),(12,12),(13,13),...
; (out)  INSERT INTO records (id, data) VALUES (20,20),(21,21),(22,22),(23,23),...
; (out)  INSERT INTO records (id, data) VALUES (30,30),(31,31),(32,32),(33,33),...
; (out)  INSERT INTO records (id, data) VALUES (40,40),(41,41),(42,42),(43,43),...
; (out)  INSERT INTO records (id, data) VALUES (50,50),(51,51),(52,52),(53,53),...
; (out)  INSERT INTO records (id, data) VALUES (60,60),(61,61),(62,62),(63,63),...
; (out)  INSERT INTO records (id, data) VALUES (70,70),(71,71),(72,72),(73,73),...
; (out)  INSERT INTO records (id, data) VALUES (80,80),(81,81),(82,82),(83,83),...
; (out)  INSERT INTO records (id, data) VALUES (90,90),(91,91),(92,92),(93,93),...
; (out)  INSERT INTO records (id, data) VALUES (100,100),(101,101),(102,102),(1...
; (out)  INSERT INTO records (id, data) VALUES (110,110),(111,111),(112,112),(1...
; (out)  INSERT INTO records (id, data) VALUES (120,120),(121,121),(122,122),(1...
; (out)  INSERT INTO records (id, data) VALUES (130,130),(131,131),(132,132),(1...
; (out)  INSERT INTO records (id, data) VALUES (140,140),(141,141),(142,142),(1...
; (out)  INSERT INTO records (id, data) VALUES (150,150),(151,151),(152,152),(1...
; (out)  INSERT INTO records (id, data) VALUES (160,160),(161,161),(162,162),(1...
; (out)  INSERT INTO records (id, data) VALUES (170,170),(171,171),(172,172),(1...
; (out)  INSERT INTO records (id, data) VALUES (180,180),(181,181),(182,182),(1...
; (out)  INSERT INTO records (id, data) VALUES (190,190),(191,191),(192,192),(1...
; (out)  INSERT INTO records (id, data) VALUES (200,200),(201,201),(202,202),(2...
; (out)  INSERT INTO records (id, data) VALUES (210,210),(211,211),(212,212),(2...
; (out)  INSERT INTO records (id, data) VALUES (220,220),(221,221),(222,222),(2...
; (out)  INSERT INTO records (id, data) VALUES (230,230),(231,231),(232,232),(2...
; (out)  INSERT INTO records (id, data) VALUES (240,240),(241,241),(242,242),(2...
; (out)  INSERT INTO records (id, data) VALUES (250,250),(251,251),(252,252),(2...
; (out)  INSERT INTO records (id, data) VALUES (260,260),(261,261),(262,262),(2...
; (out)  INSERT INTO records (id, data) VALUES (270,270),(271,271),(272,272),(2...
; (out)  INSERT INTO records (id, data) VALUES (280,280),(281,281),(282,282),(2...
; (out)  INSERT INTO records (id, data) VALUES (290,290),(291,291),(292,292),(2...
; (out)  INSERT INTO records (id, data) VALUES (300,300),(301,301),(302,302),(3...
; (out)  INSERT INTO records (id, data) VALUES (310,310),(311,311),(312,312),(3...
; (out)  INSERT INTO records (id, data) VALUES (320,320),(321,321),(322,322),(3...
; (out)  INSERT INTO records (id, data) VALUES (330,330),(331,331),(332,332),(3...
; (out)  INSERT INTO records (id, data) VALUES (340,340),(341,341),(342,342),(3...
; (out)  INSERT INTO records (id, data) VALUES (350,350),(351,351),(352,352),(3...
; (out)  INSERT INTO records (id, data) VALUES (360,360),(361,361),(362,362),(3...
; (out)  INSERT INTO records (id, data) VALUES (370,370),(371,371),(372,372),(3...
; (out)  INSERT INTO records (id, data) VALUES (380,380),(381,381),(382,382),(3...
; (out)  INSERT INTO records (id, data) VALUES (390,390),(391,391),(392,392),(3...
; (out)  INSERT INTO records (id, data) VALUES (400,400),(401,401),(402,402),(4...
; (out)  INSERT INTO records (id, data) VALUES (410,410),(411,411),(412,412),(4...
; (out)  INSERT INTO records (id, data) VALUES (420,420),(421,421),(422,422),(4...
; (out)  INSERT INTO records (id, data) VALUES (430,430),(431,431),(432,432),(4...
; (out)  INSERT INTO records (id, data) VALUES (440,440),(441,441),(442,442),(4...
; (out)  INSERT INTO records (id, data) VALUES (450,450),(451,451),(452,452),(4...
; (out)  INSERT INTO records (id, data) VALUES (460,460),(461,461),(462,462),(4...
; (out)  INSERT INTO records (id, data) VALUES (470,470),(471,471),(472,472),(4...
; (out)  INSERT INTO records (id, data) VALUES (480,480),(481,481),(482,482),(4...
; (out)  INSERT INTO records (id, data) VALUES (490,490),(491,491),(492,492),(4...
; (out)  INSERT INTO records (id, data) VALUES (500,500),(501,501),(502,502),(5...
; (out)  INSERT INTO records (id, data) VALUES (510,510),(511,511),(512,512),(5...
; (out)  INSERT INTO records (id, data) VALUES (520,520),(521,521),(522,522),(5...
; (out)  INSERT INTO records (id, data) VALUES (530,530),(531,531),(532,532),(5...
; (out)  INSERT INTO records (id, data) VALUES (540,540),(541,541),(542,542),(5...
; (out)  INSERT INTO records (id, data) VALUES (550,550),(551,551),(552,552),(5...
; (out)  INSERT INTO records (id, data) VALUES (560,560),(561,561),(562,562),(5...
; (out)  INSERT INTO records (id, data) VALUES (570,570),(571,571),(572,572),(5...
; (out)  INSERT INTO records (id, data) VALUES (580,580),(581,581),(582,582),(5...
; (out)  INSERT INTO records (id, data) VALUES (590,590),(591,591),(592,592),(5...
; (out)  INSERT INTO records (id, data) VALUES (600,600),(601,601),(602,602),(6...
; (out)  INSERT INTO records (id, data) VALUES (610,610),(611,611),(612,612),(6...
; (out)  INSERT INTO records (id, data) VALUES (620,620),(621,621),(622,622),(6...
; (out)  INSERT INTO records (id, data) VALUES (630,630),(631,631),(632,632),(6...
; (out)  INSERT INTO records (id, data) VALUES (640,640),(641,641),(642,642),(6...
; (out)  INSERT INTO records (id, data) VALUES (650,650),(651,651),(652,652),(6...
; (out)  INSERT INTO records (id, data) VALUES (660,660),(661,661),(662,662),(6...
; (out)  INSERT INTO records (id, data) VALUES (670,670),(671,671),(672,672),(6...
; (out)  INSERT INTO records (id, data) VALUES (680,680),(681,681),(682,682),(6...
; (out)  INSERT INTO records (id, data) VALUES (690,690),(691,691),(692,692),(6...
; (out)  INSERT INTO records (id, data) VALUES (700,700),(701,701),(702,702),(7...
; (out)  INSERT INTO records (id, data) VALUES (710,710),(711,711),(712,712),(7...
; (out)  INSERT INTO records (id, data) VALUES (720,720),(721,721),(722,722),(7...
; (out)  INSERT INTO records (id, data) VALUES (730,730),(731,731),(732,732),(7...
; (out)  INSERT INTO records (id, data) VALUES (740,740),(741,741),(742,742),(7...
; (out)  INSERT INTO records (id, data) VALUES (750,750),(751,751),(752,752),(7...
; (out)  INSERT INTO records (id, data) VALUES (760,760),(761,761),(762,762),(7...
; (out)  INSERT INTO records (id, data) VALUES (770,770),(771,771),(772,772),(7...
; (out)  INSERT INTO records (id, data) VALUES (780,780),(781,781),(782,782),(7...
; (out)  INSERT INTO records (id, data) VALUES (790,790),(791,791),(792,792),(7...
; (out)  INSERT INTO records (id, data) VALUES (800,800),(801,801),(802,802),(8...
; (out)  INSERT INTO records (id, data) VALUES (810,810),(811,811),(812,812),(8...
; (out)  INSERT INTO records (id, data) VALUES (820,820),(821,821),(822,822),(8...
; (out)  INSERT INTO records (id, data) VALUES (830,830),(831,831),(832,832),(8...
; (out)  INSERT INTO records (id, data) VALUES (840,840),(841,841),(842,842),(8...
; (out)  INSERT INTO records (id, data) VALUES (850,850),(851,851),(852,852),(8...
; (out)  INSERT INTO records (id, data) VALUES (860,860),(861,861),(862,862),(8...
; (out)  INSERT INTO records (id, data) VALUES (870,870),(871,871),(872,872),(8...
; (out)  INSERT INTO records (id, data) VALUES (880,880),(881,881),(882,882),(8...
; (out)  INSERT INTO records (id, data) VALUES (890,890),(891,891),(892,892),(8...
; (out)  INSERT INTO records (id, data) VALUES (900,900),(901,901),(902,902),(9...
; (out)  INSERT INTO records (id, data) VALUES (910,910),(911,911),(912,912),(9...
; (out)  INSERT INTO records (id, data) VALUES (920,920),(921,921),(922,922),(9...
; (out)  INSERT INTO records (id, data) VALUES (930,930),(931,931),(932,932),(9...
; (out)  INSERT INTO records (id, data) VALUES (940,940),(941,941),(942,942),(9...
; (out)  INSERT INTO records (id, data) VALUES (950,950),(951,951),(952,952),(9...
; (out)  INSERT INTO records (id, data) VALUES (960,960),(961,961),(962,962),(9...
; (out)  INSERT INTO records (id, data) VALUES (970,970),(971,971),(972,972),(9...
; (out)  INSERT INTO records (id, data) VALUES (980,980),(981,981),(982,982),(9...
; (out)  INSERT INTO records (id, data) VALUES (990,990),(991,991),(992,992),(9...
; (out) )

(def temps [42 42 42 42 43 43 43 44 44 44 45 45 46 48 45 44 42 42 42 42 41 41])

(map count (partition-by identity temps))
;; (4 3 3 2 1 1 1 1 4 2)                      

(comment
  (partition-by identity temps))
; ((42 42 42 42)
;  (43 43 43)
;  (44 44 44)
;  (45 45)
;  (46)
;  (48)
;  (45)
;  (44)
;  (42 42 42 42)
;  (41 41))

(eduction
 (map range)       ; (() (0) (0 1) (0 1 2) (0 1 2 3) (0 1 2 3 4))
 (partition-all 2) ; ((() (0)) ((0 1) (0 1 2)) ((0 1 2 3) (0 1 2 3 4)))
 (range 6))        ; (0 1 2 3 4 5)
; ([() (0)] [(0 1) (0 1 2)] [(0 1 2 3) (0 1 2 3 4)])

(comment
  (defn partition-by [f coll]
    (lazy-seq
     (when-let [s (seq coll)]
       (let [fst (first s)
             fv  (f fst)
             run (cons fst (take-while #(= fv (f %)) (next s)))]
         (cons run (partition-by f (seq (drop (count run) s)))))))))

(defn partition-with [f coll]
  (lazy-seq
   (when-let [s (seq coll)]
     (let [prev (first s)
           run  (cons prev (take-while #(f prev %) (next s)))] ; <2>
       (cons run (partition-with f (seq (drop (count run) s))))))))

(def events
  [{:t "2017-05-04T13:08:57Z" :msg "msg1"}
   {:t "2017-05-04T13:09:52Z" :msg "msg2"}
   {:t "2017-05-04T13:11:03Z" :msg "msg3"}
   {:t "2017-05-04T23:13:10Z" :msg "msg4"}
   {:t "2017-05-04T23:13:23Z" :msg "msg5"}])

(defn ->inst [{t :t}]
  (Instant/from (.parse DateTimeFormatter/ISO_INSTANT t)))

(defn burst? [t1 t2]
  (let [diff (.getSeconds (Duration/between (->inst t2) (->inst t1)))]
    (<= (Math/abs diff) 120)))

(partition-with burst? events)
; (({:t "2017-05-04T13:08:57Z", :msg "msg1"}
;   {:t "2017-05-04T13:09:52Z", :msg "msg2"})
;  ({:t "2017-05-04T13:11:03Z", :msg "msg3"})
;  ({:t "2017-05-04T23:13:10Z", :msg "msg4"}
;   {:t "2017-05-04T23:13:23Z", :msg "msg5"}))

(first (partition 3 (map #(do (println %) %) (range)))) ; (0 1 2)
; (out) 0
; (out) 1
; (out) 2

(first
 (sequence
  (comp (map #(do (print % ",") %))
        (partition-all 100))
  (range)))
; [0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43
;  44 45 46 47 48 49 50 51 52 53 54 55 56 57 58 59 60 61 62 63 64 65 66 67 68 69 70 71 72 73 74 75 76 77 78 79 80 81
;  82 83 84 85 86 87 88 89 90 91 92 93 94 95 96 97 98 99]

(comment
  ;; WARNING: hangs
  (first (partition-by pos? (range)))  ; (0)
  (first (sequence (partition-by pos?) (range))))
